;; @inline
ALPHA          =  %x41-5A / %x61-7A   ; A-Z / a-z

;; @inline
BIT            =  "0" / "1"

;; @inline
CHAR           =  %x01-7F
                    ; any 7-bit US-ASCII character,
                    ;  excluding NUL

;; @inline
CR             =  %x0D
                    ; carriage return

;; @inline
CRLF           =  CR LF
                    ; Internet standard newline

;; @inline
CTL            =  %x00-1F / %x7F
                    ; controls

;; @inline
DIGIT          =  %x30-39
                    ; 0-9

;; @inline
DQUOTE         =  %x22
                    ; " (Double Quote)

;; @inline
HEXDIG         =  DIGIT / "A" / "B" / "C" / "D" / "E" / "F"

;; @inline
HTAB           =  %x09
                    ; horizontal tab

;; @inline
LF             =  %x0A
                    ; linefeed

;; @inline
LWSP           =  *(WSP / CRLF WSP)
                    ; Use of this linear-white-space rule
                    ;  permits lines containing only white
                    ;  space that are no longer legal in
                    ;  mail headers and have caused
                    ;  interoperability problems in other
                    ;  contexts.
                    ; Do not use when defining mail
                    ;  headers and use with caution in
                    ;  other contexts.

;; @inline
NL             =  [CR] LF
                    ; Non-standard: Catches newlines across
                    ; operating systems.

;; @inline
OCTET          =  %x00-FF
                    ; 8 bits of data

;; @inline
SP             =  %x20

;; @inline
VCHAR          =  %x21-7E
                    ; visible (printing) characters

;; @inline
WSP            =  SP / HTAB
                    ; white space
