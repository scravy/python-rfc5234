;; @entrypoint
;; @emit
rulelist       =  1*( rule / (*WSP c-nl) )

;; @emit
rule           =  rulename defined-as elements c-nl
                    ; continues if next line starts
                    ;  with white space

;; @emit
rulename       =  ALPHA *(ALPHA / DIGIT / "-")

defined-as     =  *c-wsp ("=/" / "=") *c-wsp
                    ; basic rules definition and
                    ;  incremental alternatives

;; @emit
elements       =  alternation *WSP

;; @inline
c-wsp          =  WSP / (c-nl WSP)

;; @inline
c-nl           =  comment / NL
                    ; comment or newline
                    ; non-standard change: Uses NL instead of CRLF

;; @inline
comment        =  ";" *(WSP / VCHAR) NL
                    ; non-standard change: Uses NL instead of CRLF

;; @emit
alternation    =  concatenation
               *(*c-wsp "/" *c-wsp concatenation)

;; @emit
concatenation  =  repetition *(1*c-wsp repetition)

;; @emit
repetition     =  [repeat] element

;; @emit
repeat         =  *DIGIT "*" *DIGIT / 1*DIGIT

element        =  rulename / group / option /
               char-val / num-val / prose-val

group          =  "(" *c-wsp alternation *c-wsp ")"

;; @emit
option         =  "[" *c-wsp alternation *c-wsp "]"

; begin rfc7405

; without rfc7405:
; char-val       =  DQUOTE *(%x20-21 / %x23-7E) DQUOTE
;                     ; quoted string of SP and VCHAR
;                     ;  without DQUOTE

;; @emit
char-val       =  case-insensitive-string /
                  case-sensitive-string

;; @inline
case-insensitive-string =
                  [ "%i" ] quoted-string

;; @inline
case-sensitive-string =
                  "%s" quoted-string

;; @inline
quoted-string  =  DQUOTE *(%x20-21 / %x23-7E) DQUOTE
                    ; quoted string of SP and VCHAR
                    ;  without DQUOTE

; end rfc7405

;; @emit
num-val        =  "%" (bin-val / dec-val / hex-val)

;; @inline
bin-val        =  "b" 1*BIT
               [ 1*("." 1*BIT) / ("-" 1*BIT) ]
                    ; series of concatenated bit values
                    ;  or single ONEOF range

;; @inline
dec-val        =  "d" 1*DIGIT
               [ 1*("." 1*DIGIT) / ("-" 1*DIGIT) ]

;; @inline
hex-val        =  "x" 1*HEXDIG
               [ 1*("." 1*HEXDIG) / ("-" 1*HEXDIG) ]

prose-val      =  "<" *(%x20-3D / %x3F-7E) ">"
                    ; bracketed string of SP and VCHAR
                    ;  without angles
                    ; prose description, to be used as
                    ;  last resort
